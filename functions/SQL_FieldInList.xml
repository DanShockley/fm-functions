<fmxmlsnippet type="FMObjectList">
	<CustomFunction id="52" functionArity="2" visible="True" parameters="fieldRef;theList" name="SQL_FieldInList">
		<Calculation><![CDATA[// SQL_FieldInList ( fieldRef ; theList )
// version: 1.3, Erik Shagdar, NYHTC

/*
	Generates the entire IN clause. Values are properly quoted based on the field type.


REQUIRES:
	DateAsISO8601
	SQL_EscapeList
	SQL_Field
	ValueApplyToList


HISTORY:
	1.3 - 2019-08-01 ( eshagdar ): change dates to ISO8601 format.
	1.2.2 - 2019-03-14 ( dshockley ): Replaced pilcrow with Char ( 13 ). Remove FileName call (not needed). Uses SQL_EscapeList for text, so that internal single-quotes are handled properly. 
	1.2.1 - 2018-09-06 ( eshagdar ): updated parameters attribute ( XML ).
	1.2 - 2018-02-20 ( eshagdar ): logic is inpired by ThisFieldInListSQL function which this function used to a wrapper for. This function now additionally handles date and timestamp fields ( instead of just text and number ).
	1.1 - 2016-09-29 ( eshagdar ): added documentation.
	1.0 - 201x-xx-xx ( dshockley ): first created - wrapper for ThisFieldInListSQL.
*/


Let ( [ 
		functionsUsedViaIndirection =  If ( True ; True ; DateAsISO8601 ( "" ) ) ;


		SQ = "'" ;
		CR = char ( 13 ) ;
		fieldType = FieldType ( "" ; GetFieldName ( fieldRef ) ) ;

		/* prefix needed for dates/times: */
		valPrefix = Case (
			Position ( fieldType ; "date" ; 1 ; 1 ) ;
			"DATE " & SQ ;

			Position ( fieldType ; "timestamp" ; 1 ; 1 ) ;
			"TIMESTAMP " & SQ ;

			/* DEFAULT */
			""
		) ;

		/* dates need to be converted to ISO8601 format */
		theList = If ( Position ( fieldType ; "date" ; 1 ; 1 );
			ValueApplyToList ( "DateAsISO8601 ( [t] )" ; theList ; CR ) ;
			theList
		) ;
		valSuffix = If ( IsEmpty ( valPrefix ) ; 
			"" ; 
			SQ 
		) ;
		formattedList = If ( Position ( fieldType ; "text" ; 1 ; 1 ) ;
			SQL_EscapeList ( theList ) ;
			valPrefix & Substitute ( theList ; [ CR ; valSuffix & "," & valPrefix ] ) & valSuffix
		)
	] ; 

	SQL_Field ( fieldRef ) & " IN ( " & formattedList & " )"
)]]>
		</Calculation>
	</CustomFunction>
</fmxmlsnippet>
