<fmxmlsnippet type="FMObjectList">
	<CustomFunction id="151" functionArity="2" visible="True" parameters="resultType;resultDescription" name="ResultOfScript">
		<Calculation><![CDATA[// ResultOfScript ( resultType; resultDescription )
// version 1.1.6, Daniel A. Shockley & Erik Shagdar, NYHTC

/*
	Quickly provide script result (for use in Exit Script) to return this script's result in dictionary form, using NYHTC standards.


REQUIRES:
	#
	CallStack


HISTORY:
	1.1.6 - 2018-03-16 ( eshagdar ): help message typo
	1.1.5 - 2017-11-29 ( eshagdar ): add devMsg only if the tag is used.
	1.1.4 - 2017-11-15 ( eshagdar ): fix incorrectly specified resultType.
	1.1.3 - 2017-08-22 ( eshagdar ): added ExecutionTimestamp, ExecutionAccount keys to return dict
	1.1.2 - 2017-05-18 ( eshagdar ): added #DEVMSG
	1.1.1 - 2017-01-26 ( eshagdar ): updated MissingParams description.
	1.1 - 2017-01-17 ( eshagdar ): updated with default values. added as standard function. added to script template.
	1.0 - 2012-xx-xx ( dshockley ): created.
*/


If ( resultType = "help" or resultDescription = "help";
	/* display the help text */
	List (
		"Return a dictionary of the standard key-value pairs.";
		"    'ResultType' can take on one of the standard results - 'Success', 'Error', or 'Cancel' ( defaults to 'Success' if none is specified ).";
		"    'ResultDescription' uses a pre-canned description for specified resultType, or passes through the specified string.";
		" ";
		"  NOTE: Specifying '#DEVMSG' tag in the resultDescription will append the description to notify the developers where the error occured.";
		"  NOTE: resultType can use a special value of 'MissingParams' which will be converted to 'Error' type and with pre-canned description. You can optionally specify a comma-delimitted list of of missing params in the resultDescription to get them added to the description. the '#DEVMSG' tag is automatically appended for 'MissingParams'"
	);


	/* return a dictionary */
	Let ( [
			SQ = "'";
			devMsg = "Please contact your database administrator and give them the following information.¶CallStack:¶" & CallStack ( "DisplayCurrent" );
			str_ScriptAndFile = SQ & Get ( ScriptName ) & SQ & " script in " & SQ & Get ( FileName ) & SQ & " file";


			/* capture incorrectly specified resultType */
			resultType = if ( resultType = "MissingParam"; 
				"MissingParams"; 
				resultType 
			);

			_result = Case ( 
				resultType = "MissingParams";
				"Error";

				not IsEmpty ( resultType ); 
				resultType;

				"Success"
			);
			_description = Case ( 
				resultType = "MissingParams";
				str_ScriptAndFile & " was called without required parameters" & 
						If ( not IsEmpty ( resultDescription );
								": " & Substitute ( resultDescription; [ ¶; ", " ] );
								""
						) &
						". ";

				not IsEmpty ( resultDescription ); 
				resultDescription;

				_result = "Success";
				"Completed " & str_ScriptAndFile & ".";

				_result = "Cancel";
				"User canceled " & str_ScriptAndFile & ".";

				_result = "Error";
				"Encountered error in " & str_ScriptAndFile & ".";

				"Unable to set result description because a non-standard ResultType was specified without a ResultDescription."
			);
			_description = 
				Substitute ( 
					_description; 
					[ "#DEVMSG"; devMsg ]
				)
		];

			# ( "Result"; _result ) 
		&	# ( "ResultDescription"; _description )
		&	# ( "FileName" ; Get ( FileName ) ) 
		&	# ( "ScriptName" ; Get ( ScriptName ) ) 
		&	# ( "ExecutionTimestamp" ; Get ( CurrentTimestamp ) )
		&	# ( "ExecutionAccount" ; Get ( AccountName ) )
	)
)]]>
		</Calculation>
	</CustomFunction>
</fmxmlsnippet>