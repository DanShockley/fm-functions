<fmxmlsnippet type="FMObjectList">
	<CustomFunction id="560" functionArity="0" visible="True" parameters="" name="UUID_HTC">
		<Calculation><![CDATA[// UUID_HTC (  ) 
// version 2019-12-17

/* 
	Generates a 41-DIGIT decimal UUID number by converting FileMaker's Get ( UUID ) 
	hex-as-text and then prepending/appending digits as needed. 


NOTE: 
	The reason we use EXACTLY 41 digits is so that the SyncServerPro auditing solution 
	does not mangle large numbers into (truncated value) scientific notation for storage 
	in the SSP AuditLog table. So, we take a guaranteed-UUID from FileMaker's built-in 
	function, convert that to a decimal number, then pad the result. We (somewhat arbitrarily) 
	chose to being with "9" just to indicate this is not one of Jeremy Bante's UUID functions. 
	We _deliberately_ avoid having the next digit be a "9" so that we can be sure to never 
	begin a UUID with "9999", which would ALSO break SSP. The other are random so that a user 
	comparing two UUIDs can easily refer to either the first few digits or the last few digits 
	to tell UUIDs apart. 

	Calculation structure below is to add each higher-power converted-to-decimal digit 
	to the next-lower decimal digit before multiplying by 16. This avoids the need to 
	redundantly calculate powers of 16.
	

HISTORY: 
	2019-12-17 ( dshockley, eshagdar ): First created. Based on HexUUIDToBase10, found at  https://community.filemaker.com/thread/183271 

*/

Let ( [
		  _str = Upper ( Substitute ( Get ( UUID ) ; [ "-" ; "" ] ) )  /* ensure plain hex */
		; _hex = "0123456789ABCDEF"
	] ;

	Left ( 
		"9" 
			& Round ( 8 * Random ; 0 ) 
			& (((((((((((((((((((((((((((((((
				( Position ( _hex ; Middle ( _str ;  1 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  2 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  3 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  4 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  5 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  6 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  7 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  8 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ;  9 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 10 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 11 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 12 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 13 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 14 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 15 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 16 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 17 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 18 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 19 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 20 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 21 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 22 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 23 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 24 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 25 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 26 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 27 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 28 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 29 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 30 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 31 ; 1 ) ; 1 ; 1 ) - 1 ) ) * 16 +
				( Position ( _hex ; Middle ( _str ; 32 ; 1 ) ; 1 ; 1 ) - 1 )
			& Right ( Random ; 8 ) 
		; 41 
		)
)]]></Calculation>
	</CustomFunction>
</fmxmlsnippet>